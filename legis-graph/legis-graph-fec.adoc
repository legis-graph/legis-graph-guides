= Legis-graph + FEC filings

== The Data Model

image::https://raw.githubusercontent.com/legis-graph/legis-graph/master/img/datamodel.png[float=right]

//The data for this example comes from
//
//This is the basic data model:

//* a `+(:Intermediary)-[:INTERMEDIARY_OF]->(:Entity)+`
//* an `+(:Officer)-[:OFFICER_OF]->(:Entity)+`
//* an `+(:Officer)-[:SHAREHOLDER_OF]->(:Entity)+`
//* an `+(:Officer)-[:REGISTERED_ADDRESS]->(:Address)+`
//* an `+()-[:SIMILAR_NAME_AND_ADDRESS]->()+`


== Understanding how data load works (1) 

Run this example on an empty database to avoid unexpected clashes with existing data. Also you may want to copy the data files locally on your import directory.

=== Exploring the raw data

You can just have a look at the first few rows of your csv file without actually doing any data load

[source,cypher]
----
LOAD CSV FROM "https://dl.dropboxusercontent.com/u/67572426/fec/cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
RETURN row
----

Let's start by loading the states. They are in the seventh column (but count starts at zero) so for now, let's ignore the rest

[source,cypher]
----
LOAD CSV FROM "https://dl.dropboxusercontent.com/u/67572426/fec/cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
RETURN row[6]
----

=== Creating nodes

You want to create a node in your graph for every record in your file. The keyword in Cypher is 'CREATE'

[source,cypher]
----
LOAD CSV FROM "https://dl.dropboxusercontent.com/u/67572426/fec/cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
CREATE (s:State { code: row[6] }) 
----

Check what the previous load has produced... something unexpected?


== Understanding how data load works (2) 

Duplicates! We want a single node representing a state so we can then link all cities in the state to it...
Let's delete the states we have created and start again

[source,cypher]
----
MATCH (s:State) DELETE s 
----


The keyword in Cypher is MERGE (semantics: CREATE if it does not exist yet, MATCH if it is already there)

[source,cypher]
----
LOAD CSV FROM "https://dl.dropboxusercontent.com/u/67572426/fec/cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
MERGE (s:State { code: row[6] }) 
----

The same query with MERGE instead of CREATE should have done the job.
Now let's do the same with the cities in the sixth column. The same record may contain multiple entities.

[source,cypher]
----
LOAD CSV FROM "https://dl.dropboxusercontent.com/u/67572426/fec/cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
MERGE (s:State { code: row[6] }) 
MERGE (c:City { name: row[5] }) 
----

And finally, we are ready to connect cities with states, we just need to add the named relationship

[source,cypher]
----
LOAD CSV FROM "https://dl.dropboxusercontent.com/u/67572426/fec/cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
MERGE (s:State { code: row[6] }) 
MERGE (c:City { name: row[5] }) 
MERGE (c)-[:LOCATED_IN]->(s)
----

Now you can remove the limit to run the script on the whole dataset 
And you can have a look at the entities you've just created using the same graph pattern

[source,cypher]
----
MATCH (c:City)-[:LOCATED_IN]->(s:State)
RETURN c,s LIMIT 15
----

== Understanding how data load works: Exercises

* Using the same file (cm.txt) try to create nodes representing committees identified by the committee ID in the first column (count starts at zero) and treasurers identified by their name in the third column and connect them to indicate that the committee has this person as treasurer.

[source,cypher]
----
LOAD CSV FROM "https://dl.dropboxusercontent.com/u/67572426/fec/cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
RETURN row[0] AS committeId, row[2] AS treasurerName
----

* Extend your load script with more attributes of the committe using the keyword SET (Cypher refcard)

[source,cypher]
----
LOAD CSV FROM "https://dl.dropboxusercontent.com/u/67572426/fec/cm.txt" AS row FIELDTERMINATOR "|"
WITH row limit 10
...
MERGE (c:Committee ...
SET c.name = row[...]
----

(watch out for unexpected problems when run on the whole data set, raw data is never perfect)

* Using the ccl.txt file now, load legislators and link them to the committees that fund them.




== Adding FEC data

These queries won't actually work because the data has already been loaded, but this shows how you can load CSV data into Neo4j.


=== Add FEC Committees
[source,cypher]
----
// FEC Committees
USING PERIODIC COMMIT
LOAD CSV FROM "https://dl.dropboxusercontent.com/u/67572426/fec/cm.txt" AS row FIELDTERMINATOR "|"
WITH row
//WITH replace(row[0], "[", "") AS committee
MERGE (c:FECCommittee {committee_id: replace(row[0], "[", "")})
SET c.name = row[1],
    c.designation = row[8],
    c.committee_type = row[9],
    c.committee_party = row[10],
    c.category = row[12]
WITH row WHERE row[2] IS NOT NULL
MERGE (t:Treasurer {name:row[2]})
CREATE UNIQUE (t)-[:TREASURER_FOR]->(c);
----

=== Link candidates to committees
[source,cypher]
----
// Link candidates to committees
USING PERIODIC COMMIT
LOAD CSV FROM "https://dl.dropboxusercontent.com/u/67572426/fec/ccl.txt" AS row FIELDTERMINATOR "|" WITH row
MATCH (c:FECCommittee) WHERE c.committee_id = row[3]
MATCH (l:Legislator) WHERE l.fecIDs CONTAINS toString(row[0])
CREATE UNIQUE (c)-[:FUNDS]->(l);
----

=== Individual contributions to committees
[source,cypher]
----
// Individual contributions to committees
USING PERIODIC COMMIT
LOAD CSV FROM "https://dl.dropboxusercontent.com/u/67572426/fec/itcont.txt"
AS row FIELDTERMINATOR "|"
WITH row WHERE row[7] IS NOT NULL AND row[12] IS NOT NULL AND row[11] IS NOT NULL AND row[8] IS NOT NULL AND row[9] IS NOT NULL
MATCH (c:FECCommittee) WHERE c.committee_id = replace(row[0], "[", "")
CREATE (con:Contribution {sub_id: replace(row[20], "]", "")})
SET con.amount = toFloat(row[14]),
   con.date = row[13]
CREATE UNIQUE (con)-[:MADE_TO]->(c)
MERGE (t:Contributor {name: row[7]})
MERGE (occupation:Occupation {name: row[12]})
MERGE (employer:Employer {name: row[11]})
MERGE (city:City {name: row[8]})
MERGE (state:State {code: row[9]})
CREATE UNIQUE (t)-[:MADE_CONTRIBUTION]->(con)
CREATE UNIQUE (t)-[:HAS_OCCUPATION]->(occupation)
CREATE UNIQUE (t)-[:WORKS_FOR]->(employer)
CREATE UNIQUE (t)-[:LIVES_IN]->(city)
CREATE UNIQUE (city)-[:LOCATED_IN]->(state)
----

== Examine a single contribution
[source,cypher]
----
// FEC - examine single contribution
MATCH (l:Legislator)-[:FUNDS]-(f:FECCommittee)<-[:MADE_TO]-(c:Contribution)<-[:MADE_CONTRIBUTION]-(cont:Contributor) WITH l, f, c, cont SKIP 4 LIMIT 2
MATCH (cont)-[:LIVES_IN]->(city:City)
MATCH (cont)-[:WORKS_FOR]->(e:Employer)
MATCH (cont)-[:HAS_OCCUPATION]->(o:Occupation)
RETURN *
----

== FEC Contributions for a certain occupation by party
[source,cypher]
----
// FEC - Contributions for a certain occupation by party
MATCH (o:Occupation) WHERE o.name CONTAINS "BINGO"
MATCH (o)-[:HAS_OCCUPATION]-(con:Contributor)-[:MADE_CONTRIBUTION]->(cont:Contribution)-[:MADE_TO]->(com:FECCommittee)-[:FUNDS]->(cand:Legislator)
RETURN sum(cont.amount) AS total, cand.party ORDER BY total DESC
// cand.firstName + " " + cand.lastName AS name
----


== What companies' employees made most contributions?
[source,cypher]
----
// FEC - What companies' employees made most contributions
MATCH (e:Employer)<-[:WORKS_FOR]-(:Contributor)-[:MADE_CONTRIBUTION]-(cont:Contribution)
WITH sum(cont.amount) AS total, e
RETURN e, total ORDER BY total DESC LIMIT 100
----

== Renaissance technologies
[source,cypher]
----
// FEC - Renaissance Technologies contributions
MATCH (e:Employer {name: "RENAISSANCE TECHNOLOGIES"})<-[:WORKS_FOR]-(cont:Contributor)-[:MADE_CONTRIBUTION]->(c:Contribution)-[:MADE_TO]->(f:FECCommittee)-[:FUNDS]->(l:Legislator), (o:Occupation)-[:HAS_OCCUPATION]-(cont)
RETURN * LIMIT 30
----

== Contributions for LA legislators
[source,cypher]
----
// legis-graph-FEC - Contributions for LA legislators
MATCH (s:State {code:"LA"})<-[:REPRESENTS]-(l:Legislator)
MATCH (l)<-[:FUNDS]-(:FECCommittee)-[:MADE_TO]-(c:Contribution)
RETURN l.firstName + " " + l.lastName AS legislator,
   sum(c.amount) AS total ORDER BY total DESC;
----

== Contributions for LA legislators by employer
[source,cypher]
----
// legis-graph-FEC - Contributions for LA legislators by Employer
MATCH (s:State {code:"LA"})<-[:REPRESENTS]-(l:Legislator)
MATCH (l)<-[:FUNDS]-(:FECCommittee)-[:MADE_TO]-(c:Contribution)
MATCH (c)<-[:MADE_CONTRIBUTION]-(cont:Contributor)-[:WORKS_FOR]-(e:Employer)
RETURN sum(c.amount) AS total, e ORDER BY total DESC
----

== Contributions for LA legislators by occupation
[source,cypher]
----
// legis-graph-FEC - Contributions for LA legislators by Occupation
MATCH (s:State {code:"LA"})<-[:REPRESENTS]-(l:Legislator)
MATCH (l)<-[:FUNDS]-(:FECCommittee)-[:MADE_TO]-(c:Contribution)
MATCH (c)<-[:MADE_CONTRIBUTION]-(cont:Contributor)-[:HAS_OCCUPATION]->(o:Occupation)
RETURN sum(c.amount) AS total, o ORDER BY total DESC
----
